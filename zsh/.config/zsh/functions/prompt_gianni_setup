function +vi-git_status {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]='%F{red}*%f'
  fi
}

function prompt_gianni_precmd {
  if (( $+functions[vcs_info] )); then
    vcs_info
  fi
}

function prompt_gianni_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_gianni_precmd

  zstyle ':vcs_info:*' enable git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:(hg*|git*):*' get-revision true
  zstyle ':vcs_info:(hg*|git*):*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '%F{red}*%f'
  zstyle ':vcs_info:*' unstagedstr '%F{red}!%f'
  zstyle ':vcs_info:*' formats '%F{blue}(%f%F{red}%b%f%c%u%F{blue})%f '
  zstyle ':vcs_info:*' actionformats '%F{blue}(%f%F{red}%b%f%c%u|%F{cyan}%a%f%F{blue})%f '
  zstyle ':vcs_info:(hg*):*' branchformat '%F{red}%b%f|%F{cyan}%r%f'
  zstyle ':vcs_info:hg*:*' get-bookmarks true
  zstyle ':vcs_info:hg*:*' hgrevformat "%h"
  zstyle ':vcs_info:git*+set-message:*' hooks git_status

  # Define prompts.
  PROMPT='%~ ${vcs_info_msg_0_}%# '
  RPROMPT=''
}

prompt_gianni_setup "$@"
